buildscript {
  // Buildscript is evaluated before everything else so we can't use getExtOrDefault
  def kotlin_version = rootProject.ext.has("kotlinVersion") ? rootProject.ext.get("kotlinVersion") : project.properties["TicketmasterIgnite_kotlinVersion"]

  repositories {
    google()
    mavenCentral()
  }

  dependencies {
    classpath "com.android.tools.build:gradle:7.2.1"
    // noinspection DifferentKotlinGradleVersion
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

def isNewArchitectureEnabled() {
  return rootProject.hasProperty("newArchEnabled") && rootProject.getProperty("newArchEnabled") == "true"
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

if (isNewArchitectureEnabled()) {
  apply plugin: "com.facebook.react"
}

def getExtOrDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties["TicketmasterIgnite_" + name]
}

def getExtOrIntegerDefault(name) {
  return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties["TicketmasterIgnite_" + name]).toInteger()
}

def supportsNamespace() {
  def parsed = com.android.Version.ANDROID_GRADLE_PLUGIN_VERSION.tokenize('.')
  def major = parsed[0].toInteger()
  def minor = parsed[1].toInteger()

  // Namespace support was added in 7.3.0
  return (major == 7 && minor >= 3) || major >= 8
}

android {
  if (supportsNamespace()) {
    namespace "com.ticketmasterignite"

    sourceSets {
      main {
        manifest.srcFile "src/main/AndroidManifest.xml"
        // Exclude codegen files to avoid duplicates
        java {
          exclude '**/com/facebook/react/viewmanagers/*SafeArea*'
          exclude '**/com/facebook/react/viewmanagers/*Screen*'
          exclude '**/com/facebook/react/viewmanagers/RNS*'
          exclude '**/com/facebook/react/viewmanagers/RNGesture*'
          exclude '**/com/facebook/fbreact/specs/*SafeArea*'
          exclude '**/com/facebook/fbreact/specs/*Screen*'
          exclude '**/com/facebook/fbreact/specs/*Gesture*'
        }
      }
    }
  }

  // Add generated sources for Fabric (New Architecture)
  sourceSets {
    main {
      java.srcDirs += [
        "build/generated/source/codegen/java"
      ]
    }
  }

  compileSdkVersion getExtOrIntegerDefault("compileSdkVersion")

  defaultConfig {
    minSdkVersion getExtOrIntegerDefault("minSdkVersion")
    targetSdkVersion getExtOrIntegerDefault("targetSdkVersion")
  }

  buildFeatures {
    dataBinding = true
  }

  buildTypes {
    release {
      minifyEnabled false
    }
  }

  lintOptions {
    disable "GradleCompatible"
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }
}

repositories {
  mavenCentral()
  google()
}

def kotlin_version = getExtOrDefault("kotlinVersion")

dependencies {
  // For < 0.71, this will be from the local maven repo
  // For > 0.71, this will be replaced by `com.facebook.react:react-android:$version` by react gradle plugin
  //noinspection GradleDynamicVersion
  implementation "com.facebook.react:react-android"
  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  implementation 'androidx.compose.material:material:1.7.4'
  implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
  implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.8.1'
  implementation 'com.google.code.gson:gson:2.11.0'

  implementation 'com.ticketmaster.tickets:tickets:3.14.2'
  implementation 'com.ticketmaster.tickets:secure-entry:1.5.1'
  implementation 'com.ticketmaster.retail:purchase:3.8.3'
  implementation 'com.ticketmaster.retail:prepurchase:3.8.3'
  implementation 'com.ticketmaster.retail:discoveryapi:3.8.3'
  implementation 'com.ticketmaster.retail:foundation:3.8.3'
  implementation 'androidx.compose.material3:material3:1.4.0'

  testImplementation 'junit:junit:4.13.2'
  testImplementation 'org.mockito:mockito-core:4.1.0'
  testImplementation 'org.mockito:mockito-inline:4.1.0'
  testImplementation "io.mockk:mockk:1.12.0"
}
